# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           github 1.0

set emacs_version   30.2

github.setup        jdtsmith emacs-mac ${emacs_version} emacs-mac-
name                emacs-mac-app
conflicts           emacs-app emacs-app-devel
revision            0
categories          aqua editors
maintainers         roshanshariff

description         Emacs Mac port

long_description    ${name} is the \"Mac port\" of GNU Emacs ${emacs_version}. \
                    This provides a native GUI with tight OS integration.

platforms           {darwin >= 14}
license             GPL-3+

github.tarball_from archive

depends_lib         port:ncurses \
                    port:libxml2 \
                    path:lib/pkgconfig/gnutls.pc:gnutls \
                    port:lcms2 \
                    port:gmp

patchfiles          patch-src_emacs.c.diff

if {${subport} eq ${name}} {
    conflicts-prepend   emacs-mac-app-devel
}

post-patch {
    reinplace "s|__PREFIX__|${prefix}|" ${worksrcpath}/src/emacs.c
}

universal_variant   no

use_autoreconf      yes
autoreconf.cmd      ./autogen.sh
autoreconf.args     all

depends_build       port:autoconf \
                    port:automake \
                    port:libtool \
                    path:bin/pkg-config:pkgconfig \
                    port:texinfo \
                    port:sqlite3 \
                    port:webp

set rpaths [list]

# The Mac port uses CoreText instead of HarfBuzz
configure.args      --disable-silent-rules \
                    --with-mac \
                    --enable-mac-app=${worksrcpath} \
                    --enable-mac-self-contained \
                    --without-dbus \
                    --without-gconf \
                    --without-libotf \
                    --without-m17n-flt \
                    --without-harfbuzz \
                    --without-imagemagick \
                    --without-rsvg \
                    --without-xaw3d \
                    --without-tree-sitter \
                    --with-libgmp \
                    --with-gnutls \
                    --with-xml2 \
                    --with-lcms2 \
                    --with-modules \
                    --with-sqlite3 \
                    --with-webp \
                    --with-native-compilation=no

# Later-on this could potentially be a variant, given enough demand. See:
# https://github.com/emacs-mirror/emacs/commit/4c6f45fa8eef1a15d5790c1f3d3e608b548015db#diff-731c852b7c27c6ee90c44c871e2988ecfc2d225bc720156d7a47c1d563c2683aR59
configure.args-append  --disable-gc-mark-trace

platform darwin {
    configure.cflags-append -fobjc-arc
}

pre-configure {
    if {[llength $rpaths] > 0} {
        configure.ldflags-append "-Wl,-rpath " [join $rpaths " -Wl,-rpath "]
    }
}

subport ${name}-devel {
    set emacs_version   30.2
    set date            2025-09-28

    github.setup    jdtsmith emacs-mac 35ecf096d230577930bc97fbfe4d6ca7f37baa70
    version         [string map {- {}} ${date}]
    revision        0

    long_description \
        ${name} is the \"Mac port\" of GNU Emacs ${emacs_version}. \
        This provides a native GUI with tight OS integration.

    conflicts-prepend   emacs-mac-app

    if {[variant_isset nativecomp]} {
        notes "emacs devel subports don't always keep compatibility for native\
 compiled files. Better to cleanup your ~/.emacs.d/.local/cache/eln"
    }
}

destroot.destdir {}

post-destroot {
    set appname [expr {[variant_isset renameapp] ? "EmacsMac" : "Emacs"}]

    move ${worksrcpath}/Emacs.app ${destroot}${applications_dir}/${appname}.app

    # Install site-start.el
    set site_lisp ${destroot}${applications_dir}/${appname}.app/Contents/Resources/site-lisp
    xinstall -d ${site_lisp}
    file copy ${filespath}/site-start.el ${site_lisp}
    reinplace "s|__PREFIX__|${prefix}|g" ${site_lisp}/site-start.el

    # Install emacs-module.h header file
    xinstall -m 0755 -d ${destroot}${prefix}/include/emacs-mac
    xinstall -m 0644 ${worksrcpath}/src/emacs-module.h \
        ${destroot}${prefix}/include/emacs-mac
}

variant multitty description {Enable Multi-TTY support} {
    patchfiles-append   ${subport}-multi-tty.patch
}

variant rsvg description {Enable librsvg bindings} {
    depends_lib-append     path:lib/pkgconfig/librsvg-2.0.pc:librsvg
    configure.args-delete  --without-rsvg
    configure.args-append  --with-rsvg
}

variant imagemagick description {Enable ImageMagick bindings} {
    depends_lib-append     port:ImageMagick
    configure.args-delete  --without-imagemagick
    configure.args-append  --with-imagemagick
}

variant nativecomp description {Builds emacs with native compilation support} {
    set gcc_v                      15
    depends_lib-append             port:gcc${gcc_v}
    configure.args-append          --with-native-compilation=aot
    compiler.cpath-prepend         ${prefix}/include/gcc${gcc_v}
    compiler.library_path-prepend  ${prefix}/lib/gcc${gcc_v}
    lappend rpaths                 ${prefix}/lib/gcc${gcc_v}
}

variant treesitter description {Builds emacs with tree-sitter support} {
    configure.args-delete   --without-tree-sitter
    configure.args-append   --with-tree-sitter
    depends_lib-append  port:tree-sitter

    lappend rpaths ${prefix}/lib

    depends_run-append \
        port:tree-sitter-typescript \
        port:tree-sitter-javascript \
        port:tree-sitter-tsx \
        port:tree-sitter-c \
        port:tree-sitter-cpp \
        port:tree-sitter-java \
        port:tree-sitter-python \
        port:tree-sitter-css \
        port:tree-sitter-json \
        port:tree-sitter-c-sharp \
        port:tree-sitter-bash \
        port:tree-sitter-dockerfile \
        port:tree-sitter-cmake \
        port:tree-sitter-toml \
        port:tree-sitter-go \
        port:tree-sitter-go-mod \
        port:tree-sitter-yaml \
        port:tree-sitter-rust \
        port:tree-sitter-ruby \
        port:tree-sitter-html \
        port:tree-sitter-heex \
        port:tree-sitter-elixir \
        port:tree-sitter-lua \
        port:tree-sitter-php \
        port:tree-sitter-phpdoc

    notes-append "
To install tree-sitter grammar libraries not required by built-in *-ts-modes,\
please use M-x treesit-install-language-grammar. For details, please refer to\
etc/NEWS or the Emacs Lisp reference manual.
"
}

variant metal description {Enable experimental Metal support} {
    configure.args-append  --with-mac-metal
}

variant renameapp description {Rename app bundle to avoid conflict with emacs-app} {
    conflicts-delete    emacs-app emacs-app-devel
}

default_variants-append +nativecomp +treesitter +renameapp

if {${subport} eq ${name}} {
    livecheck.regex     {"emacs-mac-([0-9.]+)"}
    livecheck.version   ${version}
}
